use "sdl.fc"
use "foam.fc"
use "button.fc"
use "rectangle.fc"
use "image.fc"
use "composite.fc"
use "column.fc"

import i32 puts(string str);

import void printnum(i32 num);

struct State
{
    counter: 0
};

struct SideBarButton : Item
{
	icon
};

struct SideBar : Item
{
	items
};

struct SideBarItem
{
	icon
};

function expand(SideBarButton item)
{
	return Rectangle
	{
		x: item.x,
		y: item.y,
		width: item.width,
		height: item.height,
		color: rgb(210, 51, 81),
		children: [
			Image
			{
				x: 25,// item.x + ((item.width - 50) / 2),
				y: item.y + ((item.height - 50) / 2),
				width: 50,
				height: 50,
				source: item.icon
			}
		]
	};
}

function createSideBarButton(SideBarItem item)
{
	return LayoutItem {
		stretch: 0,
		minimum: 100,
		item: SideBarButton
		{
			icon: item.icon
		}
	};
}

function itemsFor((SideBarItem item, ...items))
{
	return createSideBarButton(item) -> itemsFor(...items);
}

function itemsFor(SideBarItem item)
{
	return createSideBarButton(item);
}

function expand(SideBar item)
{
	return Rectangle {
		width: item.width,
		height: item.height,
		color: rgb(210, 51, 81),
		children: [
			Column
			{
				children: itemsFor(item.items)
			}
		]
	};
}

function increaseCounter(State state)
{
    return state with
    {
        counter: state.counter + 1
    };
}

function decreaseCounter(State state)
{
    return state with
    {
        counter: state.counter - 1
    };
}

struct ToolBar : Item
{
	items
};

struct ToolBarItem : Item
{
	text,
	image
};

struct IconSource
{
	name
}

function icon(string source)
{
	return IconSource
	{
		name: source
	};
}

function itemsFor((ToolBarItem item, ...items))
{
	return itemsFor(item) -> itemsFor(...items);
}

function itemsFor(ToolBarItem item)
{
	return LayoutItem
	{
		minimum: 100,
		item: Row
		{
			children: [
				LayoutItem
				{
					minimum: 30,
					item: Image
					{
						source: item.image
					}
				}
			]
		}
	};
}

function expand(ToolBar toolBar)
{
	return Rectangle
	{
		width: toolBar.width,
		height: toolBar.height,
		color: window,
		children: [
			Row
			{
				children: itemsFor(toolBar.items)
			}
		]
	};
}

global window: rgb(224, 224, 224)
global light: rgb(255, 255, 255)

function application(State state)
{
    return Column
    {
		spacing: 0,
		children: [
			LayoutItem
			{
				minimum: 30,
				item: ToolBar
				{
					items: [
						ToolBarItem
						{
							text: "Save",
							image: "/usr/share/icons/Adwaita/24x24/actions/document-save-symbolic.symbolic.png"
						},
						ToolBarItem
						{
							text: "Add actor",
							image: "/usr/share/icons/Adwaita/24x24/actions/list-add-symbolic.symbolic.png"
						},
						ToolBarItem
						{
							text: "Create easing",
							image: "/usr/share/icons/Adwaita/24x24/actions/join-symbolic.symbolic.png"
						},
						ToolBarItem
						{
							text: "Load guides",
							image: "/usr/share/icons/Adwaita/24x24/actions/show-guides-symbolic.symbolic.png"
						},
						ToolBarItem
						{
							text: "Settings",
							image: "/usr/share/icons/Adwaita/24x24/actions/configure-symbolic.symbolic.png"
						}
					]
				}
			},
			LayoutItem
			{
				stretch: 1,
				item: Rectangle
				{
					color: light
				}
			},
			LayoutItem
			{
				minimum: 30,
				item: Rectangle
				{
					color: window
				}
			}
		]
    };
}

function initState()
{
    return State {};
}

export i32 main()
{
    let definition = ApplicationDefinition
    {
        state: initState,
        ui: application,
    };

    return Foam::run(definition);
}
