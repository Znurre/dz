import int puts(string str);

import int SDL_Init(uint flags);
import int SDL_SetRenderDrawColor(long renderer, int r, int g, int b, int a);
import int SDL_RenderClear(long renderer);
import int SDL_RenderFillRect(long renderer, SDL_Rect rect);
import int SDL_PollKeyEvent(SDL_KeyboardEvent key);
import int SDL_PollMouseMotionEvent(SDL_MouseMotionEvent motion);

import long SDL_CreateRenderer(long window, int index, int flags);
import long SDL_CreateWindow(string title, int x, int y, int w, int h, int flags);

import void SDL_RenderPresent(long renderer);
import void SDL_PumpEvents();

import EventState SDL_WaitEventEx(EventState events);

import void printnum(int num);

global SDL_INIT_VIDEO: 0x00000020u;
global SDL_WINDOWPOS_UNDEFINED: 0x1FFF0000u;
global SDL_KEYDOWN: 0x300u;
global SDL_KEYUP: 0x301u;
global SDLK_a: 0x61u;
global SDLK_d: 0x64u;
global SDLK_s: 0x73u;
global SDLK_w: 0x77u;

struct SDL_Rect
{
    int x: 0,
    int y: 0,
    int width: 0,
    int height: 0
};

struct SDL_KeyboardEvent
{
    int type: 0,
    int timestamp: 0,
    int windowID: 0,
    int padding: 0,
    int scancode: 0,
    int sym: 0,
    long padding: 0L
};

struct SDL_MouseMotionEvent
{
    int type : 0,
    int timestamp : 0,
    int windowID: 0,
    int which: 0,
    int state: 0,
    int x: 0,
    int y: 0,
    int xrel: 0,
    int yrel: 0
};

global Normal: 0;
global MouseOver: 1;
global Pressed: 2;

struct Item
{
    x: 0,
    y: 0,
    width: 0,
    height: 0,
    state: 0,
    children: []
};

struct Button : Item
{
    text
};

struct Color
{
    r: 0,
    g: 0,
    b: 0
}

struct Rectangle : Item
{
    color: Color {}
};

struct State
{
    counter: 0
};

struct ControlTemplate
{
    normal,
    mouseOver,
    pressed
};

struct EventState
{
    SDL_KeyboardEvent key,
    SDL_MouseMotionEvent motion
};

struct ApplicationState
{
    events,
    user
};

function createEventState()
{
    return EventState
    {
        key: SDL_KeyboardEvent {},
        motion: SDL_MouseMotionEvent {}
    };
}

function createApplicationState(State state)
{
    return ApplicationState
    {
        events: SDL_WaitEventEx(createEventState()),
        user: state
    };
}

function rgb(int r, int g, int b)
{
    return Color
    {
        r: r,
        g: g,
        b: b
    };
}

function buttonTemplate(Button button)
{
    return ControlTemplate
    {
        normal: Rectangle
        {
            x: button.x,
            y: button.y,
            width: button.width,
            height: button.height + 2,
            color: rgb(41, 128, 185),
            children: [
                Rectangle
                {
                    x: button.x,
                    y: button.y,
                    width: button.width,
                    height: button.height,
                    color: rgb(29, 153, 243)
                }
            ]
        },
        mouseOver: Rectangle
        {
            x: button.x,
            y: button.y,
            width: button.width,
            height: button.height + 2,
            color: rgb(41, 128, 185),
            children: [
                Rectangle
                {
                    x: button.x,
                    y: button.y,
                    width: button.width,
                    height: button.height,
                    color: rgb(61, 174, 233)
                }
            ]
        },
        pressed: Rectangle
        {
            x: button.x,
            y: button.y,
            width: button.width,
            height: button.height + 2,
            color: rgb(41, 128, 185)
        }
    };
}

function application(State state)
{
    return [
        Button
        {
            x: 10 + state.counter,
            y: 10,
            width: 70,
            height: 20,
            text: "Click me!"
        },
        Button
        {
            x: 10 + state.counter,
            y: 40,
            width: 70,
            height: 20,
            text: "Click me!"
        },
        Button
        {
            x: 10 + state.counter,
            y: 70,
            width: 70,
            height: 20,
            text: "Click me!"
        },
        Button
        {
            x: 10 + state.counter,
            y: 100,
            width: 70,
            height: 20,
            text: "Click me!"
        }
    ];
}

function boundingRect(Item item)
{
    return SDL_Rect
    {
        x: item.x,
        y: item.y,
        width: item.width,
        height: item.height
    };
}

function clear(long renderer, State state)
{
    SDL_SetRenderDrawColor(renderer, 255, 255, 255, 255);
    SDL_RenderClear(renderer);

    return state;
}

function present(long renderer, State state)
{
    SDL_RenderPresent(renderer);

    return state;
}

function setRenderDrawColor(long renderer, Color color)
{
    return SDL_SetRenderDrawColor(renderer, color.r, color.g, color.b, 255);
}

function draw_s(long renderer, without item)
{
    return nothing;
}

function draw_s(long renderer, Rectangle rectangle)
{
    setRenderDrawColor(renderer, rectangle.color);

    SDL_RenderFillRect(renderer, boundingRect(rectangle));

    return draw_s(renderer, rectangle.children);
}

function selectTemplate(ControlTemplate template, Item item)
{
    if (item.state == MouseOver)
    {
        return template.mouseOver;
    }

    if (item.state == Pressed)
    {
        return template.pressed;
    }

    return template.normal;
}

function drawButton(long renderer, Button button)
{
    draw_s(renderer, selectTemplate(buttonTemplate(button), button));

    return button;
}

function containsMouse(Item item, SDL_MouseMotionEvent motion)
{
    return (motion.x > item.x)
        && (motion.x < (item.x + item.width))
        && (motion.y > item.y)
        && (motion.y < (item.y + item.height));
}

function isPressed(SDL_MouseMotionEvent motion)
{
    return motion.state == 1;
}

function updateButton(EventState events, Button button)
{
    if (containsMouse(button, events.motion))
    {
        if (isPressed(events.motion))
        {
            return button with
            {
                state: Pressed
            };
        }

        return button with
        {
            state: MouseOver
        };
    }

    return button with
    {
        state: Normal
    };
}

iterator function update(ApplicationState state, Button button)
{
    return updateButton(state.events, button);
}

iterator function update(ApplicationState state, (Button button, ...controls))
{
    return updateButton(state.events, button) -> (state, ...controls);
}

iterator function draw(long renderer, State state, Button button)
{
    return button;
}

iterator function draw(long renderer, State state, (Button button, ...controls))
{
    return drawButton(renderer, button) -> (renderer, state, ...controls);
}

function run(State state, Item item)
{
    return state;
}

function run(State state, (Item item, ...items))
{
    return run(state, ...items);
}

function initState()
{
    return State {};
}

function mainLoop(long renderer, State state)
{
    return mainLoop(renderer
        , present(renderer
            , run(state
                , draw(renderer
                    , clear(renderer, state)
                    , update(createApplicationState(state)
                        , application(state)
                        )
                    )
                )
            )
        );
}

export int main()
{
    SDL_Init(SDL_INIT_VIDEO);

    return mainLoop(
        SDL_CreateRenderer(
            SDL_CreateWindow("test", 0, 0, 800, 600, 0), 0, 0)
            , initState()
        );
}
