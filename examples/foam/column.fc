use "array.fc"
use "algorithms.fc"

import f32 intToFloat(int value);
import int floatToInt(f32 value);

// Types

struct Layout : Item
{
	spacing: 5
};

struct LayoutItem
{
	item,
	stretch: 0,
	minimum: 0,
	maximum: 32767
};

struct Column : Layout;
struct Row : Layout;

// Utility

struct LayoutInfo
{
	totalStretch: 0.0,
	numberOfItems: 0,
};

function buildLayoutInfo(LayoutInfo info, Item item)
{
	return info with
	{
		totalStretch: info.totalStretch + intToFloat(item.stretch),
		numberOfItems: info.numberOfItems + 1,
	};
}

function getHeight(Item item)
{
	return item.height;
}

function getHeight(LayoutItem item)
{
	return getHeight(item.item);
}

function getStretch(LayoutItem item)
{
	return intToFloat(item.stretch);
}

function getNativeSize(Column column)
{
	return column.height;
}

function getNativeSize(Row row)
{
	return row.width;
}

function calculateNativeItemSize(LayoutItem item, int availableSize, f32 totalStretch)
{
	if (item.stretch > 0)
	{
		let distributed = intToFloat(availableSize) * (intToFloat(item.stretch) / totalStretch);

		return floatToInt(distributed);
	}

	return item.minimum;
}

// Draw

function draw(long renderer, without parent, Layout item)
{
	return draw(renderer, item, item.children);
}

function draw(long renderer, Item parent, Layout item)
{
	let surrogate = parent with
	{
		x: parent.x + item.x,
		y: parent.y + item.y
	};

	return draw(renderer, surrogate, item.children);
}

function draw(long renderer, any parent, LayoutItem item)
{
	return draw(renderer, parent, item.item);
}

// Process

function process(ApplicationState state, (Layout item, ...controls))
{
	return tail process(state, ...controls);
}

function process(ApplicationState state, Layout item)
{
	return state.user;
}

// Update

function update(EventState events, Layout item, Layout nextItem)
{
	let seed = LayoutInfo {};

	let info = Array::aggregate(item.children, seed, buildLayoutInfo);

	return item with
	{
		children: layout(events, item, 0, info, item.children, nextItem.children)
	};
}

function update(EventState events, (Layout item, ...controls), (Layout nextItem, ...nextControls))
{
	return tail update(events, item, nextItem) -> update(events, ...controls, ...nextControls);
}

// Column

function layout(EventState events, Column column, int offset, LayoutInfo info, LayoutItem item, LayoutItem nextItem)
{
	let spacing = (info.numberOfItems - 1) * column.spacing;
	let availableSize = getNativeSize(column) - spacing;
	let layouted = item.item with
	{
		y: offset,
		width: column.width,
		height: calculateNativeItemSize(item, availableSize, info.totalStretch),
	};

	return item with
	{
		item: tail update(events, layouted, nextItem.item)
	};
}

function layout(EventState events, Column column, int offset, LayoutInfo info, (LayoutItem item, ...items), (LayoutItem nextItem, ...nextItems))
{
	let layouted = tail layout(events, column, offset, info, item, nextItem);

	return layouted -> layout(events, column, getHeight(layouted) + offset + column.spacing, info, ...items, ...nextItems);
}

// Row

function layout(EventState events, Row row, int offset, LayoutInfo info, LayoutItem item, LayoutItem nextItem)
{
	let spacing = (info.numberOfItems - 1) * row.spacing;
	let availableSize = getNativeSize(row) - spacing;
	let layouted = item.item with
	{
		x: offset,
		width: calculateNativeItemSize(item, availableSize, info.totalStretch),
		height: row.height,
	};

	return update(events, layouted, nextItem);
}

function layout(EventState events, Row row, int offset, LayoutInfo info, (LayoutItem item, ...items), (LayoutItem nextItem, ...nextItems))
{
	let spacing = (info.numberOfItems - 1) * row.spacing;
	let availableSize = getNativeSize(row) - spacing;
	let width = calculateNativeItemSize(item, availableSize, info.totalStretch);
	let layouted = item.item with
	{
		x: offset,
		width: width,
		height: row.height
	};

	return update(events, layouted, nextItem) -> layout(events, row, width + offset + row.spacing, info, ...items, ...nextItems);
}

// Common

function layout(EventState events, Item item, int offset, LayoutInfo info, (LayoutItem item, ...items), LayoutItem nextItem)
{
	return nothing;
}

function layout(EventState events, Item item, int offset, LayoutInfo info, LayoutItem item, (LayoutItem nextItem, ...nextItems))
{
	return nothing;
}
